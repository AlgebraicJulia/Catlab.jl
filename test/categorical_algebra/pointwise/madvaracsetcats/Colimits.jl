module TestMADVarACSetColimits 

using Test, Catlab

cat = ACSetCategory(WeightedGraph{Symbol}());
G2 = @acset WeightedGraph{Symbol} begin 
  V=2; E=2; src=[1,2]; tgt=[1,2]; weight=[:X, :Y] 
end
clim = coproduct[cat](G2,G2);
universal[cat](clim, Cospan(id[cat](G2), id[cat](G2)))

############
# LoneAttr #
############

@present SchLoneAttr(FreeSchema) begin X::AttrType end
@acset_type LoneAttr(SchLoneAttr, part_type=BitSetParts){Bool}
const â„’ = ACSetCategory(MADVarACSetCat(LoneAttr()))

X = @acset LoneAttr begin X = 4 end
Y = @acset LoneAttr begin X = 4 end

"""
                      g              f
                Y  <-----  X   ---->  Y 
               ---        ---        ---
               [3] <----- [1]  ----> [1]
                                     
                âŠ¥  <----- [2]  ---->  âŠ¥     
      [1] [2]                                 [2]  [4]
                âŠ¤  <----- [3]  ----> [3]
                                   
               [4] <----- [4]  ---->  [4]
"""
f = ACSetTransformation(X, Y; 
                        X=[AttrVar(1), false, AttrVar(3), AttrVar(4)], cat=â„’)
g = ACSetTransformation(X, Y; 
                        X=[AttrVar(3), false, true, AttrVar(4)], cat=â„’)

"""
Coequalization merges [1] and [3] and âŠ¥ as well as [4] and âŠ¤ 

Y: [1]  [3]  [2]  [4]
    â†˜  â†™      â†“    â†“
Q:   âŠ¤       [1]  [2]       <-- these variables represent eq classes 
                                 of variables in Y
"""
q, = coeq = coequalizer[â„’](f,g);

A = @acset LoneAttr begin X = 3 end

"""
Y: [1] [2] [3] [4]
    â†“   â†“   â†“   â†“
A:  âŠ¥   âŠ¤   âŠ¥  [3]  [1]  [2]
"""
YA = ACSetTransformation(Y, A; X=[true, true, false, AttrVar(3)], cat=â„’)

"""
So a map out of Y which sends {1,3} to âŠ¥, {2} to âŠ¤, and {4} to {3}
can be transported to a map out of the equivalence classes:
"""

@test factorize[â„’](coeq, YA)[:X].val.val.val == Dict(1=> Right(true), 2=>Left(3))

####################
# VE-labeled Graph #
####################

@present SchVELabeledGraph <: SchGraph begin
  Label::AttrType; vlabel::Attr(V,Label); elabel::Attr(E,Label)
end

@acset_type VELabeledGraph(SchVELabeledGraph,index=[:src,:tgt],part_type=BitSetParts) <: AbstractGraph

const VES = VELabeledGraph{Symbol}

const ğ’± = ACSetCategory(MADVarACSetCat(VES()))

# Coproduct
###########

g = @acset VES begin 
  V=2; E=2; Label=2; src=1; tgt=2; 
  vlabel=[AttrVar(1),:b]; elabel=[:e, AttrVar(2)] 
end

expected = @acset VES begin 
  V=4; E=4; Label=4; src=[1,1,3,3]; tgt=[2,2,4,4]
  vlabel=[AttrVar(1),:b,AttrVar(3),:b]; 
  elabel=[:e,AttrVar(2),:e,AttrVar(4)] 
end

colim = Î¹â‚, Î¹â‚‚ = coproduct[ğ’±](g, g)

@test is_isomorphic(ob(colim), expected)
@test collect(get(Î¹â‚[:Label])) == Left.(1:2)
@test collect(get(Î¹â‚‚[:Label])) == Left.(3:4)


# Coequalizers
##############

I = @acset VES begin V=2; E=1; Label=1
  src=1; tgt=2; vlabel=[:x,:x]; elabel=[AttrVar(1)]
end
VX = @acset VES begin V=1; vlabel=[:x] end
# Coequalizer in Graph: collapsing a segment to a loop.
VES(1)#; V=(vlabel=[:x],))
Î± = ACSetTransformation((V=[1], E=Int[]), VX, I; cat=ğ’±)
Î² = ACSetTransformation((V=[2], E=Int[]), VX, I; cat=ğ’±)
@test is_natural(Î±, cat=ğ’±)
@test is_natural(Î², cat=ğ’±)
prj, = coeq = coequalizer[ğ’±](Î±, Î²)

expected = @acset VES begin V=1; E=1; Label=1
  src=1; tgt=1; vlabel=[:x]; elabel=[AttrVar(1)]
end
@test ob(coeq) == expected
@test force(prj[:V]) â‰ƒ FinFunction(Dict(1=>1,2=>1))
@test force(prj[:E]) â‰ƒ FinFunction(Dict(1=>1))
@test Dict(get(prj[:Label])) == Dict(pure(FinFunction(Dict(1=>1)), Symbol))

# Pushouts
###########
g1 = @acset VES begin V=2; E=1; Label=2
  src=1; tgt=2; vlabel=[:x,AttrVar(2)]; elabel=[AttrVar(1)]
end
g2 = @acset VES begin V=1; E=1; Label=1
  src=1; tgt=1; vlabel=[:x]; elabel=[AttrVar(1)]
end

Î± = ACSetTransformation((V=[1], E=Int[]), VX, g1; cat=ğ’±)
Î² = ACSetTransformation((V=[1], E=Int[]), VX, g2; cat=ğ’±)
@test is_natural(Î±; cat=ğ’±) 
@test is_natural(Î²; cat=ğ’±)
colim = pushout[ğ’±](Î±, Î²);
@test nv(ob(colim)) == 2
@test src(ob(colim)) == [2,2]
@test tgt(ob(colim)) == [1,2]
@test is_natural(coproj1(colim); cat=ğ’±)
@test is_natural(coproj2(colim); cat=ğ’±)

#################
# WeightedGraph #
#################

@acset_type WG(SchWeightedGraph, part_type=BitSetParts){Bool} <: AbstractGraph
const ğ’ = ACSetCategory(MADVarACSetCat(WG()))


"""
A graph with two loops:      âŠ¤â†»â€¢â†º[1]       [2]
and an unbound Weight attrvar
"""
A = @acset WG begin 
  V=1; E=2; Weight=2; src=1; tgt=1; weight=[AttrVar(1),true] 
end

# Initial object 
################

i = initial[ğ’]()

@test dom(create[ğ’](A)) == WG()


B = @acset WG begin 
  V=1; E=1; Weight=1; src=1; tgt=1; weight=[AttrVar(1)] 
end
@test dom(create[ğ’](B)) == WG()


# COPRODUCT
###########

"""
A graph with two copies of two loops:      âŠ¤â†»â€¢â†º[2]   âŠ¤â†»â€¢â†º[4]     [1]  [3]
and two unbound Weight attrvars
"""
expected = @acset WG begin 
  V=2; E=4; Weight=4; src=[1,1,2,2]; tgt=[1,1,2,2]; 
  weight=[true,AttrVar(2), true, AttrVar(4)]
end

Î¹â‚,Î¹â‚‚ = AA = coproduct[ğ’](A,A);

@test is_isomorphic(ob(AA), expected)


"""
Two maps from A+A to a graph X:  âŠ¤â†»â€¢   [1]  [2]
These only have one choice for their vertex and edge and first weight variable, however they have freedom over their second weight variable.
f sends it to [1], g sends it to [2]
"""
X = @acset WG begin V=1;E=1;Weight=2;src=1;tgt=1;weight=[true] end
fâ€² = homomorphism(A, X; initial=(Weight=[true,AttrVar(1)],), cat=ğ’)
gâ€² = homomorphism(A, X; initial=(Weight=[true,AttrVar(2)],), cat=ğ’)

""" Thus copairing f and g takes these two maps A -> X 
and gives one map A + A -> X
"""
fg = copair[ğ’](AA, fâ€², gâ€²);
@test is_natural(fg; cat=ğ’)
@test force(compose[ğ’](Î¹â‚, fg); cat=ğ’) == force(fâ€²; cat=ğ’)
@test force(compose[ğ’](Î¹â‚‚, fg); cat=ğ’) == force(gâ€²; cat=ğ’)

# PUSHOUTS
##########

""" A has two loops, and two variables (one floating, one on a loop) 

We can merge the two variables into one by coequalizing a map that picks out just the loop variable and another that picks out the free variable
"""
V = @acset WG begin Weight=1 end
f = ACSetTransformation(Dict(:Weight=>[AttrVar(1)]), V, A, cat=ğ’)
g = ACSetTransformation(Dict(:Weight=>[AttrVar(2)]), V, A, cat=ğ’)

q, = coeq = coequalizer[ğ’](f, g);

@test get(q[:Weight]).(1:2) == [Left(1), Left(1)]
@test nparts(ob(coeq), :Weight) == 1


""" If we just coproducted A, we'd get two pairs of loops and two 
floating variables. But by pushing out with the above maps we merge the free variable from one of the copies of A with the loop variable of the other """
Î¹â‚,Î¹â‚‚ = clim = colimit[ğ’](Span(f,g));

@test nparts(ob(clim), :Weight) == 3


@test is_natural(Î¹â‚;  cat=ğ’)
@test is_natural(Î¹â‚‚; cat=ğ’)

fâ€² = homomorphism(A, X; initial=(Weight=[true,AttrVar(2)],), cat=ğ’)
gâ€² = homomorphism(A, X; initial=(Weight=[true,true],), cat=ğ’)
# fg = universal[ğ’](clim, Cospan(fâ€²,gâ€²; cat=ğ’)) # NOT YET WORKING

# @test collect(fg[:Weight]) == [Right(true), Left(2), Right(true)]

end # module
